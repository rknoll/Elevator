apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'jacoco'

group = 'at.fhhagenberg.sqe.project'
version = '0.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

idea {
    project {
        jdkName = '1.8'
        languageLevel = '1.8'
    }
}

buildscript {
    repositories { mavenCentral() }
    dependencies { classpath 'org.ajoberstar:gradle-jacoco:0.2.0' }
}

if (hasProperty('teamcity') && !property('teamcity')['build.number'].startsWith(version + "-")) {
    version += '-' + property('teamcity')['build.number']
    println "##teamcity[buildNumber '" + version + "']"
} else if (hasProperty('teamcity')) {
    version = property('teamcity')['build.number']
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Elevator', 'Implementation-Version': version, 'Main-Class': 'at.fhhagenberg.sqe.project.ElevatorProgram'
    }
}

test.finalizedBy jacocoTestReport

uploadArchives {
    repositories {
        ivy {
            credentials {
                username = System.getenv('IVY_USERNAME')
                password = System.getenv('IVY_PASSWORD')
            }
            url System.getenv('IVY_HOST')
            layout 'maven'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.+'
    testCompile group: 'org.easymock', name: 'easymock', version: '3.1'
}

uploadArchives {
    repositories {
       flatDir {
           dirs 'repos'
       }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}

task javadocs(type: Javadoc) {
    source = sourceSets.main.allJava
}

task javadocsZip(type: Zip, dependsOn: javadocs) {
    classifier = 'javadoc'
    from javadocs.destinationDir
    archiveName 'SQE-Project-Elevator-javadoc.zip'
}

artifacts {
    archives javadocsZip
}
